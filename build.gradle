plugins {
    id 'idea'
    id 'application'
    id 'maven-publish'
    id 'nebula.release' version '16.0.0'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

group = 'dev.tomek'

ext {
    sourceCompatibility = 17
    picoCliVersion = '4.6.2'
    lombokVersion = '1.18.22'
    jacksonVersion = '2.13.0'
    mainClassName = 'dev.tomek.userinstaller.intellij.idea.IdeaInstaller'
    // for phpstorm
//    mainClassName = 'dev.tomek.userinstaller.intellij.phpstorm.PhpStormInstaller'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "info.picocli:picocli:$picoCliVersion"
    annotationProcessor "info.picocli:picocli-codegen:$picoCliVersion"
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    testImplementation platform('org.junit:junit-bom:5.8.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.21.0'
}

application {
    mainClass = project.mainClassName
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    manifest {
        attributes([
            'Main-Class'         : project.mainClassName,
            "Application-Version": project.version,
            "Application-Commit" : grgit.head().id
        ])

    }
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE"
        exclude "module-info.class"
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/24570105/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Deploy-Token'
                value = 'NqqBLQ225viSUndtM87Q'
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
